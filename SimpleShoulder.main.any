// Todo: Write a small description of your model here

Main = {
  
  // The actual body model goes in this folder
  AnyFolder MyModel = {
    
    AnyVar rg = 0.04;
    AnyVar rh = 0.03;
    
    // Global Reference Frame
    AnyFixedRefFrame GlobalRef = {
      
      // Todo: Add points for grounding of the model here
      //Definitions of muscles insertions  
      AnyRefNode SpringAnchor = {
        sRel = {0.194, 0.215, 0.0};
      };
    };
    
    
    
    //Definition of the glenoid fossea
    AnySeg Glenoid = 
    {
      r0 = {0.194, 0.415, 0.0}; // similaire au centre HH 
      Mass = 0;
      Jii = {0.0, 0.0, 0.0};
      //      AnyDrawSeg drw = {};
      
      //Define a sphere that represent the glenoid fossa. Contact constraint will be define between 
      // the sphere and the sphere representing the humeral head. 
      AnyRefNode GlenoidCenter = {
        sRel = {0.01,0,0};
        AnySurfSphere GlenoidFossa = 
        {
          Radius = Main.MyModel.rg;
          AnyDrawParamSurf drw = {
            RGB = {0, 0.6, 0.6};
            Opacity = 0.5;   
          };
        };
      };
      
      //Muscles insertions
      AnyRefNode DeltoidIns = {
        sRel = {0.0, 0.045, 0.0};
        AnyDrawNode drw = {
          Opacity = 0.5;
        };
      };           
      
      AnyRefNode SupraIns = {
        sRel = {-0.115, 0.019, 0.0};
        AnyDrawNode drw = {
          Opacity = 0.5;
        };
      };  
      
      
    }; // end of glenoid fossa
    
    //Definition of the humerus segment 
    AnySeg Humerus = 
    {
      r0 = {0.194, 0.215, 0.0};
      Mass = 2.1;
      Jii = {0.021, 0, 0.021};
      AnyDrawSeg drw = {};
      
      AnyRefNode HumerusCG = {
        AnyDrawRefFrame drw = {};
      };
      
      //Muscle Origine
      AnyRefNode DeltoidOrg = {
        sRel = {0.005, 0.045, 0.0};
        AnyDrawNode drw = {
          Opacity = 0.5;
        };
      }; 
      AnyRefNode SupraOrg = {
        sRel = {0.03, 0.21, 0.0};
        AnyDrawNode drw = {
          Opacity = 0.5;
        };
      };
      
      
      // Define a sphere representing the center of humeral head. The supraspinatus and deltoid muscles        
      // will wrap about this sphere.
      AnyRefNode HumerusHeadCenter = {
        sRel = {0,0.2, 0};
        
        AnySurfSphere HumerusHead = 
        {
          Radius = Main.MyModel.rh;
          AnyDrawParamSurf drw = {
            RGB = {0, 0, 1};
            Opacity = 0.5;
            
          };
        };
      };
      
    };// end of humerus
    
    //Muscles
    AnyFolder Muscle = {  
      
      AnyMuscleShortestPath Deltoid = {
        AnyMuscleModel MuscleModel = {
          F0 = 200;
        };
        AnyRefFrame &org = Main.MyModel.Humerus.DeltoidOrg;
        AnyParamSurf &wrap = Main.MyModel.Humerus.HumerusHeadCenter.HumerusHead;
        AnyRefFrame &ins = Main.MyModel.Glenoid.DeltoidIns;
        SPLine = {
          StringMesh = 50;
        };
        AnyDrawMuscle drw = {
          RGB = {0.7, 0, 0};
          Opacity = 0.5;
          
        };    
      };
      
      AnyMuscleShortestPath Supra = {
        AnyMuscleModel MuscleModel = {
          F0 = 200;
        };
        AnyRefFrame &org = Main.MyModel.Humerus.SupraOrg;
        AnyParamSurf &wrap = Main.MyModel.Humerus.HumerusHeadCenter.HumerusHead;
        AnyRefFrame &ins = Main.MyModel.Glenoid.SupraIns;
        SPLine = {
          StringMesh = 50;
        };
        AnyDrawMuscle drw = {
          RGB = {0.7, 0, 0};
          Opacity = 0.5;
          
        };    
      };
      
    }; // end of muscle 
    
    //    Definition of the scapulo thoracic joint. No shoulder rythme.
    AnyRevoluteJoint STJoint = 
    {
      AnyRefFrame &ground = Main.MyModel.GlobalRef;
      AnyRefFrame &Glen = Main.MyModel.Glenoid;
    };
    AnyKinEqSimpleDriver STdriver = 
    {
      AnyRevoluteJoint &ST = Main.MyModel.STJoint;
      DriverVel = {0};
    };
    
    
    // GH joint defined as a length constraint on a pline through both centers
    AnyKinEqSimpleDriver GHJoint = {
      AnyKinPLine pline = {
        AnyRefFrame &ori = Main.MyModel.Glenoid.GlenoidCenter;
        AnyRefFrame &ins = Main.MyModel.Humerus.HumerusHeadCenter;
      };
      DriverPos = {0.04 - 0.03};
      DriverVel = {0.0};
    };
    
    // FDK driver to represent the two possible movements of the humeral head
    AnyKinEqSimpleDriver gh = {
      AnyKinLinear lin = {
        AnyRefFrame &g = Main.MyModel.Glenoid.GlenoidCenter;
        AnyRefFrame &ins = Main.MyModel.Humerus.HumerusHeadCenter;
      };
      MeasureOrganizer = {1,2};
      DriverPos = {0,0};
      DriverVel = {0,0};
      CType = {ForceDep, ForceDep};
      Reaction.Type = {Off, Off};
    };
    
    //     Define springs in the shoulder, simulating the effect of passive elements
    AnyForce ShoulderPE = {   
      AnyKinPLine pline = 
      {
        AnyRefFrame &ori = Main.MyModel.GlobalRef.SpringAnchor;
        AnyRefFrame &ins = Main.MyModel.Humerus.HumerusHeadCenter;
        AnyDrawPLine drw = {
          Thickness = 0.005;
          RGB = {0,1,0};
        };
      };
      F = {-1000*(pline.Pos[0]-0.05)};
      
    };
    
    // Shoulder driver- abduction.
    AnyKinEqSimpleDriver ShoulderDriver = {
      AnyKinRotational rot = {
        AnyRefFrame &ori = Main.MyModel.Glenoid;
        AnyRefFrame &ins = Main.MyModel.Humerus;
        Type = RotVector;
      };
      DriverPos = {0.0, 0.0, 0.0};
      DriverVel = {0.0, 0.0, pi/2};
      
      
    };
    
  }; // end of my model 
  
  //   The study: Operations to be performed on the model
  AnyBodyStudy MyStudy = {
    AnyFolder &Model = .MyModel;
    Gravity = {0.0, -9.81, 0.0};
    tStart = 0;
    tEnd = 1;
    nStep = 101;
    InverseDynamics.ForceDepKinOnOff=On;
  };
  
};





